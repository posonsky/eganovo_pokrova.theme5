<?xml version="1.0" encoding="UTF-8"?>
<rules xmlns="http://namespaces.plone.org/diazo"
       xmlns:css="http://namespaces.plone.org/diazo/css"
       xmlns:xhtml="http://www.w3.org/1999/xhtml"
       xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
       xmlns:xi="http://www.w3.org/2001/XInclude">

  <!-- Include the backend theme -->
  <xi:include href="++theme++barceloneta/backend.xml" />

  <xsl:strip-space elements="*"/>

  <theme href="index.html" />
  <notheme css:if-not-content="body.frontend#visual-portal-wrapper" />

  <rules css:if-content="#portal-top">
    <!-- Attributes -->
    <copy attributes="*" css:theme="html" css:content="html" />
    <!-- Base tag -->
    <before css:theme="title" css:content="base" />
    <!-- Title -->
    <replace css:theme="title" css:content="title" />
    <!-- Pull in Plone Meta -->
    <after css:theme-children="head" css:content="head meta" />
    <!-- Don't use Plone icons, use the theme's -->
    <drop css:content="head link[rel='apple-touch-icon']" />
    <drop css:content="head link[rel='shortcut icon']" />
    <!-- CSS -->
    <after css:theme-children="head" css:content="head link" />
    <after css:theme-children="head" css:content="head style" />
    <!-- Script -->
    <after css:theme-children="head" css:content="head script" />
  </rules>

  <!-- Copy over the id/class attributes on the body tag.
   This is important for per-section styling -->
  <copy attributes="*" css:content="body" css:theme="body" />

  <!-- Portal top -->
  <!-- <replace css:theme="#portal-top" css:content-children="#portal-top" /> -->

  <!-- move global nav -->
  <!-- <xsl:variable name="portal_root"
    select="id('portaltab-index_html')/a/@href"/> -->

  <replace css:theme="a.navbar-brand">
    <a class="navbar-brand" href="{$portal_url}">
      <img src="{$portal_url}/++theme++eganovo_pokrova.theme5/images/star-logo-24.svg" />
    </a>
  </replace>

  <replace css:theme-children="ul#mainnavigation"
    css:content-children="ul#portal-globalnav" method="document" />

  <xsl:template match="id('portal-globalnav')/li">
    <xsl:if test="not(@id='portaltab-index_html')">
      <xsl:copy>
        <xsl:for-each select="@id|@role">
          <xsl:copy/>
        </xsl:for-each>
        <xsl:attribute name="class">
          <xsl:choose>
            <xsl:when
              test="contains(@class,'selected')">active nav-item selected</xsl:when>
            <xsl:otherwise>nav-item</xsl:otherwise>
          </xsl:choose>
        </xsl:attribute>
        <xsl:apply-templates select="a"/>
      </xsl:copy>
    </xsl:if>
  </xsl:template>

  <xsl:template match="a">
    <xsl:copy>
      <xsl:attribute name="class">nav-link</xsl:attribute>
      <xsl:copy-of select="@*" />
      <xsl:apply-templates />
    </xsl:copy>
  </xsl:template>

  <!-- Search form -->
  <after css:theme="#mainnavigation" css:content="#searchGadget_form" />
  <drop css:content="#searchGadget_form .searchSection" />
  <drop css:content="#searchGadget_form .hiddenStructure" />

  <xsl:template match="id('searchGadget_form')">
    <xsl:copy>
      <xsl:copy-of select="@*" />
      <xsl:attribute name="class">form-inline pull-xs-right</xsl:attribute>
      <xsl:apply-templates />
    </xsl:copy>
  </xsl:template>

  <xsl:template match="id('searchGadget')">
    <xsl:copy>
      <xsl:copy-of select="@*" />
      <xsl:attribute name="class">form-control form-control-sm</xsl:attribute>
      <xsl:apply-templates />
    </xsl:copy>
  </xsl:template>

  <xsl:template match="id('searchGadget_form')//input[@class='searchButton']">
    <xsl:copy>
      <xsl:copy-of select="@*" />
      <xsl:attribute name="class">btn btn-sm btn-secondary-outline</xsl:attribute>
      <xsl:apply-templates />
    </xsl:copy>
  </xsl:template>

  <!-- include view @@hero on homepage only
  <after css:theme="#mainnavigation-wrapper"
    css:content=".principal" href="/@@hero"
    css:if-content="body.template-document_view.section-front-page" />
  -->

  <!-- full-width breadcrumb -->
  <replace css:content="#viewlet-above-content" css:theme="#above-content" />

  <xsl:template match="id('portal-breadcrumbs')//ol">
    <xsl:copy>
      <xsl:copy-of select="@*" />
      <xsl:attribute name="class">breadcrumb</xsl:attribute>
      <xsl:apply-templates />
    </xsl:copy>
  </xsl:template>

  <xsl:template match="id('portal-breadcrumbs')//ol/li[last()]">
    <xsl:copy>
      <xsl:copy-of select="@*" />
      <xsl:attribute name="class">active</xsl:attribute>
      <xsl:apply-templates />
    </xsl:copy>
  </xsl:template>

  <!-- Alert message -->
  <replace css:theme-children="#global_statusmessage"
    css:content-children="#global_statusmessage" />

  <xsl:variable name="public_front">
    <xsl:choose>
      <xsl:when test="//body[contains(@class,'section-last-news') and contains(@class ,'userrole-anonymous')]">
        <xsl:text>true</xsl:text>
      </xsl:when>
      <xsl:otherwise>
        <xsl:text>false</xsl:text>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:variable>

  <!-- Central column -->
  <replace css:theme="#content-container" method="raw">

    <div class="col-xs-12 col-md-7">
      <div class="row">
        <div class="col-xs-12 col-md-11">

          <xsl:choose>
            <xsl:when test="$public_front = 'true'">
              <xsl:apply-templates css:select="#content-core" />
            </xsl:when>
            <xsl:otherwise>
              <xsl:apply-templates css:select="#content" />
            </xsl:otherwise>
          </xsl:choose>

        </div>
      </div>
      <footer class="row">
        <div class="col-xs-12 col-md-7">
          <xsl:copy-of css:select="#viewlet-below-content" />
        </div>
      </footer>
    </div>
  </replace>

  <drop css:content="#document-action-sendto" if="$public_front = 'true'" />

  <!-- Right column -->
  <rules css:if-content="#portal-column-one">
    <replace css:theme="#right-column-container">
      <div class="col-xs-12 col-md-5 sidebar-offcanvas">
        <aside id="right-column">
          <xsl:copy-of css:select="#portal-column-one > *" />
        </aside>
      </div>
    </replace>
  </rules>

  <!-- Footer -->
  <replace css:theme-children="#portal-footer"
    css:content-children="#portal-footer-wrapper" />

  <!-- toolbar -->
  <replace css:theme="#portal-toolbar" css:content-children="#edit-bar"
    css:if-not-content=".ajax_load" css:if-content=".userrole-authenticated" />
  <replace css:theme="#anonymous-actions"
    css:content-children="#portal-personaltools-wrapper"
    css:if-not-content=".ajax_load" css:if-content=".userrole-anonymous" />

</rules>
